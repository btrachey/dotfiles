## use jk to switch to command mode
bindkey -M viins 'jk' vi-cmd-mode
## vim commands on input
bindkey -v
## change cursor based on vim input mode
zle-keymap-select() {
  if [[ ${KEYMAP} == vicmd ]] ||
     [[ $1 = 'block' ]]; then
    echo -ne '\e[2 q'

  elif [[ ${KEYMAP} == main ]] ||
       [[ ${KEYMAP} == viins ]] ||
       [[ ${KEYMAP} = '' ]] ||
       [[ $1 = 'beam' ]]; then
    echo -ne '\e[5 q'
  fi
}

zle -N zle-keymap-select

_fix_cursor() {
  echo -ne '\e[5 q'
}

precmd_functions+=(_fix_cursor)

## search command history with up/down
autoload -U history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^[[A" history-beginning-search-backward-end
bindkey "^[[B" history-beginning-search-forward-end
bindkey -M vicmd "k" history-beginning-search-backward-end
bindkey  -M vicmd "j" history-beginning-search-forward-end

# vim-surround at the command line
autoload -Uz surround
zle -N delete-surround surround
zle -N add-surround surround
zle -N change-surround surround
bindkey -a cs change-surround
bindkey -a ds delete-surround
bindkey -a ys add-surround
bindkey -M visual S add-surround

# vim text objects inside quotes
autoload -U select-quoted
zle -N select-quoted
for m in visual viopp; do
  for c in {a,i}{\',\",\`}; do
    bindkey -M $m $c select-quoted
  done
done

# vim text objects inside brackets
autoload -U select-bracketed
zle -N select-bracketed
for m in visual viopp; do
  for c in {a,i}${(s..)^:-'()[]{}<>bB'}; do
    bindkey -M $m $c select-bracketed
  done
done

# zsh syntax highlighting
# https://github.com/zsh-users/zsh-syntax-highlighting
export ZSH_HIGHLIGHT_HIGHLIGHTERS=(main brackets)
source /opt/homebrew/share/zsh-syntax-highlighting/zsh-syntax-highlighting.zsh
# zsh auto bracketing
zsh_autopair_dir="$HOME/.zsh_plugins/zsh-autopair"
if [[ ! -d $zsh_autopair_dir ]]; then
  git clone https://github.com/hlissner/zsh-autopair "$zsh_autopair_dir"
fi
source "$zsh_autopair_dir/autopair.zsh"
autopair-init

# shift+H/L to move to beginning/end of line in vi command mode
autoload -Uz vi-beginning-of-line vi-end-of-line
bindkey -a H vi-beginning-of-line
bindkey -a L vi-end-of-line
